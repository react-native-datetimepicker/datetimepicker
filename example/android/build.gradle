buildscript {
    apply(from: {
        def searchDir = rootDir.toPath()
        do {
            def p = searchDir.resolve("node_modules/react-native-test-app/android/dependencies.gradle")
            if (p.toFile().exists()) {
                return p.toRealPath().toString()
            }
        } while (searchDir = searchDir.getParent())
        throw new GradleException("Could not find `react-native-test-app`");
    }())

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        getReactNativeDependencies().each { dependency ->
            classpath(dependency)
        }
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url({
                def searchDir = rootDir.toPath()
                do {
                    def p = searchDir.resolve("node_modules/react-native/android")
                    if (p.toFile().exists()) {
                        return p.toRealPath().toString()
                    }
                } while (searchDir = searchDir.getParent())
                throw new GradleException("Could not find `react-native`");
            }())
        }
        mavenCentral()
        google()
        maven {
            // All of Detox' artifacts are provided via the npm module
            url "$rootDir/../../node_modules/detox/Detox-android"
        }
    }
    afterEvaluate { project ->
        def androidExtension = project.extensions.findByName('android')
        if (androidExtension != null && project.name == 'app') {
            androidExtension.defaultConfig {
                ndk {
                    abiFilters 'arm64-v8a', 'x86', 'x86_64'
                }
            }

            androidExtension.signingConfigs {
                test {
                    keyAlias "androiddebugkey"
                    keyPassword "android"
                    storeFile file("debug.keystore")
                    storePassword "android"
                }
            }

            androidExtension.buildTypes.debug.signingConfig = androidExtension.signingConfigs.test
            androidExtension.buildTypes.release.signingConfig = androidExtension.signingConfigs.test
            androidExtension.testBuildType = System.getProperty('testBuildType', 'debug')

            androidExtension.sourceSets.androidTest.java.srcDirs += "$rootDir/app/src/androidTest/java"

            project.dependencies {
                androidTestImplementation('com.wix:detox:+')
            }
        }
    }
}
