buildscript {
    apply(from: {
        def searchDir = rootDir.toPath()
        do {
            def p = searchDir.resolve("node_modules/react-native-test-app/android/dependencies.gradle")
            if (p.toFile().exists()) {
                return p.toRealPath().toString()
            }
        } while (searchDir = searchDir.getParent())
        throw new GradleException("Could not find `react-native-test-app`");
    }())

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        getReactNativeDependencies().each { dependency ->
            classpath(dependency)
        }
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url({
                def searchDir = rootDir.toPath()
                do {
                    def p = searchDir.resolve("node_modules/react-native/android")
                    if (p.toFile().exists()) {
                        return p.toRealPath().toString()
                    }
                } while (searchDir = searchDir.getParent())
                throw new GradleException("Could not find `react-native`");
            }())
        }
        maven {
             // All of Detox' artifacts are provided via the npm module
             url "$rootDir/../../node_modules/detox/Detox-android"
        }
        mavenCentral()
        google()
    }
    // https://github.com/microsoft/react-native-test-app/wiki/Platform-Specifics:-Android#detox
    afterEvaluate { project ->
        def androidExtension = project.extensions.findByName('android')
        if (androidExtension != null && project.name == 'app') {
            androidExtension.defaultConfig {
                ndk {
                    abiFilters 'arm64-v8a', 'x86', 'x86_64'
                }
                // this along with `androidTestImplementation`s fixes
                // Opening an oat file without a class loader. Are you using the deprecated DexFile APIs?
                testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            }

            androidExtension.sourceSets.androidTest.java.srcDirs += "$rootDir/app/src/androidTest/java"

            project.dependencies {
                androidTestImplementation('com.wix:detox:+')
                androidTestImplementation 'androidx.test:runner:1.5.2'
                androidTestImplementation 'androidx.test:rules:1.5.2'
            }
        }
    }
}
